apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

configurations {
    source {
        extendsFrom archives
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'info.cukes:cucumber-core:1.1.5'
    testCompile 'junit:junit:4.11'
}

project.tasks.uploadArchives.dependsOn 'test'

task jarSources(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task jarJavadoc(type: Jar, dependsOn: 'javadoc') {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jarSources
    archives jarJavadoc
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: project.version.contains('-SNAPSHOT') ? "https://oss.sonatype.org/content/repositories/snapshots" : "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.properties['sonatypeUsername'], password: project.properties['sonatypePassword'])
            }

            pom.project {
                name 'gradle-cucumber-plugin'
                packaging 'jar'
                description 'A Gradle plugin to run Cucumber tests'
                url 'https://github.com/samueltbrown/gradle-cucumber-plugin/'

                scm {
                    url 'scm:git@github.com:samueltbrown/gradle-cucumber-plugin.git'
                    connection 'scm:git@github.com:samueltbrown/gradle-cucumber-plugin.git'
                    developerConnection 'scm:git@github.com:samueltbrown/gradle-cucumber-plugin.git'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://raw.github.com/samueltbrown/gradle-cucumber-plugin/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'samueltbrown'
                        name 'Samuel Brown'
                    }
                    developer {
                        id 'lucas1000001'
                        name 'Matthew Lucas'
                    }
                    developer {
                        id 'viphe'
                        name 'Philippe Vosges'
                    }
                }
            }
        }
    }
}

task createWrapper(type: Wrapper) {
    gradleVersion = '1.8'
}
